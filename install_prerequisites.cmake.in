string(FIND ${CMAKE_INSTALL_PREFIX} "_CPack_Packages" run_by_cpack)
if(${run_by_cpack} EQUAL -1)
  set(run_by_cpack 0)
else()
  set(run_by_cpack 1)
endif()

include(GetPrerequisites)
get_prerequisites(@target_location@ target_prerequisites 0 0 "" "")
foreach(prerequisite ${target_prerequisites})
  gp_resolve_item(@target_location@ ${prerequisite} "" "" prerequisite_resolved)
  get_filename_component(prerequisite_name ${prerequisite_resolved} NAME)
  get_filename_component(prerequisite_realpath ${prerequisite_resolved} REALPATH)
  get_filename_component(prerequisite_realname ${prerequisite_realpath} NAME)

  if(NOT (${prerequisite_name} MATCHES "^(libc.so|libm.so|libpthread.so)"))
    if(WIN32)
      set(install_location "${CMAKE_INSTALL_PREFIX}/bin/${prerequisite_name}")
    else()
      set(install_location "${CMAKE_INSTALL_PREFIX}/lib/@PROJECT_NAME@/${prerequisite_name}")
    endif()

    execute_process(
      COMMAND ${CMAKE_COMMAND} -E compare_files
        ${prerequisite_realpath}
        ${install_location}
      RESULT_VARIABLE is_different
      OUTPUT_QUIET
      ERROR_QUIET)
    if(is_different)
      if(NOT ${run_by_cpack})
        message("-- Installing: ${install_location}")
      endif()
      execute_process(
        COMMAND ${CMAKE_COMMAND} -E copy
          ${prerequisite_realpath}
          ${install_location})
    else()
      if(NOT ${run_by_cpack})
        message("-- Up-to-date: ${install_location}")
      endif()
    endif()
    list(APPEND CMAKE_INSTALL_MANIFEST_FILES ${install_location})
  endif()
endforeach()

# On Linux, copy ld-linux
if("@CMAKE_SYSTEM_NAME@" STREQUAL "Linux")
  execute_process(
    COMMAND ldd @target_location@ | grep ld-linux
    OUTPUT_VARIABLE ldd_ld_linux
    ERROR_QUIET)
  string(REGEX MATCH "[^ \t\n]*ld-linux[^ \t\n]*\\.so[^ \t\n]*" ld_linux_realpath ${ldd_ld_linux})
  get_filename_component(ld_linux_realname ${ld_linux_realpath} NAME)
  set(ld_linux_install_location "${CMAKE_INSTALL_PREFIX}/lib/@PROJECT_NAME@/${ld_linux_realname}")
  execute_process(
    COMMAND ${CMAKE_COMMAND} -E compare_files
      ${ld_linux_realpath}
      ${ld_linux_install_location}
    RESULT_VARIABLE is_different
    OUTPUT_QUIET
    ERROR_QUIET)
  if(is_different)
    if(NOT ${run_by_cpack})
      message("-- Installing: ${ld_linux_install_location}")
    endif()
    execute_process(
      COMMAND ${CMAKE_COMMAND} -E copy
        ${ld_linux_realpath}
        ${ld_linux_install_location})
  else()
    if(NOT ${run_by_cpack})
      message("-- Up-to-date: ${ld_linux_install_location}")
    endif()
  endif()
  list(APPEND CMAKE_INSTALL_MANIFEST_FILES ${ld_linux_install_location})
endif()
